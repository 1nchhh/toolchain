;-------------------------------------------------------------------------------
; Libload launcher for shared libraries
;-------------------------------------------------------------------------------
	.def	__findlibload

	segment	launcher
	.assume	adl=1

__findlibload:
	call	020628h		; _pushop1, save running program name
__tryfind:
	ld	hl,__libloadappvar
	call	020320h		; _mov9toop1
	ld	a,015h		; saves a whole 3 bytes
	ld	(0D005F8h),a	; op1

	call	02050Ch		; _chkfindsym
	jr	c,__notfound
	call	021F98h		; _chkinram
	jr	nz,__inarc	; if in ram, archive LibLoad and search again
	call	021448h		; _arc_unarc
	jr	__tryfind
__inarc:
	push	de
	call	0205C4h		; _popop1, restore running program name
	pop	hl
	ld	de,9
	add	hl,de
	ld	e,(hl)
	add	hl,de
	inc	hl
	inc	hl
	inc	hl
	ld	de,__relocationstart
	jp	(hl)		; jump to the loader -- it should take care of everything
__notfound:
	call	0205C4h		; _popop1, restore running program name
	call	020814h		; _clrscrn
	call	020828h		; _homeup
	ld	hl,__missingappvar
	call	0207C0h		; _puts
	call 	0207F0h		; _newline
	ld	hl,__webaddress
	call	0207C0h		; _puts
	jp	020D8Ch		; _getkey
__relocationstart:		; libraries to be relocated will be placed here for the relocator
	segment	libs
	;...
	;...
	;...
	segment	data
__missingappvar:
	db	"Need"
__libloadappvar:
	db	" LibLoad",0
__webaddress:
	db	"http://tiny.cc/clibs",0
