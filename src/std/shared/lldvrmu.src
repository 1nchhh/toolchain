	assume	adl=1

	public	__lldvrmu
__lldvrmu:
; Atrociously slow.

	push	af

	ld	a, i
	di
	ex	af, af'

	ld	iy, 8
	add	iy, sp

	push	bc
	push	de
	push	hl

	ld	bc, (iy + 7)
	sbc	hl, hl
	ex	de, hl
	sbc	hl, hl
	exx
	ld	hl, (iy + 10)
	ex.s	de, hl
	ld	hl, (iy + 4)
	ld	sp, hl
	sbc	hl, hl

	ld	c, 8

.byte_loop:
	dec	iy
	ld	a, (iy - 9)

	ld	b, 8

.bit_loop:
	adc	a, a
	exx
	adc	hl, hl
	ex	de, hl
	adc	hl, hl
	ex	de, hl
	exx
	adc	hl, hl

	exx
	sbc	hl, sp
	ex	de, hl
	sbc	hl, bc
	ex	de, hl
	exx
	sbc	hl, de

	jr	nc, .add_back_skip
	exx
	add	hl, sp
	ex	de, hl
	adc	hl, bc
	ex	de, hl
	exx
	adc	hl, de
.add_back_skip:

	djnz	.bit_loop

	adc	a, a
	cpl
	ld	(iy + 12), a

	dec	c
	jr	nz, .byte_loop

	ld	sp, iy

	push	hl
	exx
	pop	bc

	ex	af, af'
	jp	po, .ei_skip
	ei
.ei_skip:

	pop	af

	ret
