	assume	adl=1

	public	__llrems
__llrems:

	scf
virtual
	jr	nc, $ + 2
	load .jr_nc: 1 from $$
end virtual
	db	.jr_nc

	public	__lldivs
__lldivs:

	or	a, a

	push	iy
	push	af

	ld	iy, 2
	add	iy, sp

	ld	a, (iy + 14)
	cp	a, $80
	jr	c, .neg_divisor_skip

	push	bc
	push	hl
	ld	c, a

	lea	hl, iy + 7
	ld	b, 8

.neg_divisor_loop:
	ld	a, 0
	sbc	a, (hl)
	ld	(hl), a
	inc	hl
	djnz	.neg_divisor_loop

	ld	a, c
	pop	hl
	pop	bc
.neg_divisor_skip:

	xor	a, b
	ld	(iy + 15), a

	pop	af
	push	af
	ld	a, b
	jr	nc, .rem_save_sign_skip
	ld	(iy + 15), a
.rem_save_sign_skip:

	add	a, a
	call	c, __llneg

	call	__lldvrmu.hijack

	pop	af
	jr	c, .div_quotient_skip
	ld	hl, (iy + 15)
	ld	de, (iy + 18)
	ld	bc, (iy + 21)
.div_quotient_skip:

	bit	7, (iy + 23)
	call	nz, __llneg

	pop	iy

	ret

	extern	__lldvrmu.hijack
	extern	__llneg
