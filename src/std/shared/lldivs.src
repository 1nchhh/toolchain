	assume	adl=1

	public	__llrems
__llrems:

	scf
virtual
	jr	nc, $ + 2
	load .jr_nc: 1 from $$
end virtual
	db	.jr_nc

	public	__lldivs
__lldivs:

	or	a, a

	push	iy
	push	af

	sbc	a, a

	ld	iy, 2
	add	iy, sp

	ld	a, b
	jr	nz, .xor_divisor_skip
	xor	a, (iy + 14)
.xor_divisor_skip:
	ld	(iy + 15), a

	bit	7, (iy + 14)
	jr	z, .neg_divisor_skip

	push	bc
	push	hl

	lea	hl, iy + 7
	ld	b, 8

.neg_divisor_loop:
	ld	a, 0
	sbc	a, (hl)
	ld	(hl), a
	inc	hl
	djnz	.neg_divisor_loop

	pop	hl
	pop	bc
.neg_divisor_skip:

	bit	7, b
	call	nz, __llneg

	call	__lldvrmu.hijack

	pop	af
	jr	c, .div_quotient_skip
	ld	hl, (iy + 15)
	ld	de, (iy + 18)
	ld	bc, (iy + 21)
.div_quotient_skip:

	bit	7, (iy + 23)
	call	nz, __llneg

	pop	iy

	ret

	extern	__lldvrmu.hijack
	extern	__llneg
